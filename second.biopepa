//Parameters
//-Functional rates
contactyy = [5e-05*Sy*Iy];
contactym = [0.0005*Sy*Im];
contactyo = [5e-05*Sy*Io];
contactmy = [4.5e-05*Sm*Iy];
contactmm = [7e-05*Sm*Im];
contactmo = [4e-05*Sm*Io];
contactoy = [2.5e-05*So*Iy];
contactom = [0.0005*So*Im];
contactoo = [1e-05*So*Io];
recoveryy = [0.005*Iy];
deathy = [0.0001*Iy];
recoverym = [0.0035*Im];
deathm = [0.00055*Im];
recoveryo = [0.0025*Io];
deatho = [0.001*Io];
//-Initial state
Sy0 = 990;
Sm0 = 490;
So0 = 500;
Iy0 = 10;
Im0 = 10;
Io0 = 0;
R0 = 0;
D0 = 0;
//Model
Sy = (contactyy,1)<<Sy + (contactym,1)<<Sy + (contactyo,1)<<Sy;
Sm = (contactmy,1)<<Sm + (contactmm,1)<<Sm + (contactmo,1)<<Sm;
So = (contactoy,1)<<So + (contactom,1)<<So + (contactoo,1)<<So;
Iy = (contactyy,1)>>Iy + (contactym,1)>>Iy + (contactyo,1)>>Iy + (contactmy,1)(+) + (contactoy,1)(+) + (recoveryy,1)<<Iy + (deathy,1)<<Iy;
Im = (contactym,1)(+) + (contactmy,1)>>Im + (contactmm,1)>>Im + (contactmo,1)>>Im + (contactom,1)(+) + (recoverym,1)<<Im + (deathm,1)<<Im;
Io = (contactyo,1)(+) + (contactmo,1)(+) + (contactoy,1)>>Io + (contactom,1)>>Io + (contactoo,1)>>Io + (recoveryo,1)<<Io + (deatho,1)<<Io;
R = (recoveryy,1)>>R + (recoverym,1)>>R + (recoveryo,1)>>R;
D = (deathy,1)>>D + (deathm,1)>>D + (deatho,1)>>D;
//System equation
Sy[Sy0] <*> Sm[Sm0] <*> So[So0] <*> Iy[Iy0] <*> Im[Im0] <*> Io[Io0] <*> R[R0] <*> D[D0]
