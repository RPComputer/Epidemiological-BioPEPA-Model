//Parameters
//-Functional rates
contactyy = [5e-05*Sy*Iy];
contactyo = [3.5e-05*Sy*Io];
contactoy = [2.5e-05*So*Iy];
contactoo = [1e-05*So*Io];
recoveryy = [0.005*Iy];
deathy = [0.0001*Iy];
recoveryo = [0.004*Io];
deatho = [0.001*Io];
//-Initial state
Sy0 = 490;
So0 = 500;
Iy0 = 10;
Io0 = 0;
R0 = 0;
D0 = 0;
//Model
Sy = (contactyy,1)<<Sy + (contactyo,1)<<Sy;
So = (contactoy,1)<<So + (contactoo,1)<<So;
Iy = (contactyy,1)>>Iy + (contactyo,1)>>Iy + (contactoy,1)(+) + (recoveryy,1)<<Iy + (deathy,1)<<Iy;
Io = (contactyo,1)(+) + (contactoy,1)>>Io + (contactoo,1)>>Io + (recoveryo,1)<<Io + (deatho,1)<<Io;
R = (recoveryy,1)>>R + (recoveryo,1)>>R;
D = (deathy,1)>>D + (deatho,1)>>D;
//System equation
Sy[Sy0] <*> So[So0] <*> Iy[Iy0] <*> Io[Io0] <*> R[R0] <*> D[D0]
