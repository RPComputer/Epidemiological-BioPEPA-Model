//Goldbeter Model for the cell cycle 


// Definition of Compartment (it is not used, so it could be avoid....)

cell = 1.e-14; 


// Definition of component properties
// _ is empty

C: 0.05,12,0.01,0.6, cell; 
M': 0.05,20,0.99,1,cell;
M: 0.05,14,0.01,0.7,cell;
X': 0.05,20,0.99,1,cell;  
X: 0.05,13,0.01,0.65,cell; 

//Auxiliary elements for degradation and creation
Res:1,1,_,_,_;
CF:1,1,_,_,_ ;


//Definition of functional rates 
f(alpha_1) =  fMA(v_i); 
f(alpha_2)  = fMA(k_d); 
f(alpha_4) =  fMM(V_2,K_2); 
f(alpha_5) =  fMM(V_3,K_3);
f(alpha_6)  = fMM(V_4,K_4); 
f(alpha_7)  = fMM(V_d,K_d); 
f(alpha_3)  = fMM'((V_1,K_c ,K_1),M',C) = ((V_1*C)/(K_c + C))*((M')/(K1 + M');


//Parameters
v_i= 0.025; 
k_d =0.01;  
V_1=12 ;
K_1= 0.02; 
V_2 = 1.5;
K_2= 0.02; 
V_3=12;  
K_3= 0.02;  
V_d= 0.0625;
V_4 =2;
K_4= 0.02;  
K_d= 0.02;
K_c =0.5;

//Definition of species components

C  = (alpha_1,1) << C + (alpha_2,1) >> C + (alpha_7,1)  >> C + (alpha_3,1) (+) C;
M' = (alpha_4,1) << M' + (alpha_3,1) >> M';
M =  (alpha_3,1) << M  + (alpha_4,1) >> M + (alpha_5,1) (+) M;
X' = (alpha_6,1) << X' + (alpha_5,1) >> X'; 
X  = (alpha_5,1) << X  + (alpha_6,1) >> X + (alpha_7,1) (+) X;
Res = (alpha_2,1) (.) Res; 
CF  = (alpha_1,1) (.) CF;

//Model component

(((((C(0) <alpha_3> M(0)) <alpha_3,alpha_4> M'(20)) 
<alpha_5,alpha_7> X(0))<alpha_5,alpha_6> X'(20))
<alpha_2> Res(0))<alpha_1>CF(1)
